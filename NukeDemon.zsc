Class NuclearCyberdemon : Cyberdemon
{
	Default
	{
		//$Category Monsters
		//$Title Nuclear Cyberdemon
		Translation "48:63=@49[255,0,0]", "144:159=@38[255,0,0]";
		Health 10000;
		Radius 70;
		Height 300;
		Mass 20000;
		PainChance 10;
		Scale 3.0;
		RipLevelMin 5;
		MaxStepHeight 96;
		+NoFear;
		+NoInfighting;
		+NeverRespawn;
		+Telestomp;
		+Frightening;
		-Pushable;
		-DontThrust;
		DamageFactor "NuclearBlast", 0.0;
		DamageFactor "NuclearRadiation", 0.1;
		Obituary "%o got nuked by a giant Cyberdemon";
	}
	States
	{
		See:
			CYBR A 3 A_Hoof;
			TNT1 A 0 A_QuakeEx (5,5,4,35/2,192,2048,"world/bossquake");
			CYBR ABBCC 3 A_Chase;
			CYBR D 3 A_Metal;
			CYBR D 3 A_Chase;
			Loop;
		Missile:
			CYBR E 8 A_FaceTarget;
			CYBR E 8 A_PlaySound ("weapons/nuclearmissilecharge",CHAN_AUTO,5.0,False,ATTN_NONE);
			CYBR E 16 A_FaceTarget;
			CYBR F 55 A_SpawnProjectile ("NuclearMissileShot",100,-36);
			Goto See;
		Death:
			CYBR H 10;
			CYBR I 10 A_Scream;
			CYBR JKL 10 A_QuakeEx (6,6,5,35*2,000,2048,"world/bossquake");
			CYBR M 10 A_NoBlocking;
			CYBR NO 10;
			CYBR P 30;
			CYBR P -1 A_BossDeath;
			Stop;
	}
}